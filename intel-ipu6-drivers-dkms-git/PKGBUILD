# Maintainer: Paul Ragot <ragot.paul12@gmail.com>
pkgname=intel-ipu6-drivers-dkms-git
_pkgname=ipu6-drivers
pkgver=r200.19c1deddd
pkgrel=1
pkgdesc="Intel IPU6 camera drivers (DKMS)"
arch=('x86_64')
url="https://github.com/intel/$_pkgname"
license=('GPL-2.0-only')
groups=('ipu6-cameras')
depends=('dkms' 'intel-ivsc-firmware-git')
makedepends=('git')
provides=('intel-ipu6-dkms-git' 'intel-ivsc-driver-dkms-git')
conflicts=('intel-ipu6-dkms-git' 'intel-ivsc-driver-dkms-git')
source=(
    "$_pkgname::git+$url.git"
    '70-intel-ipu6-psys.rules'
)
sha256sums=(
    'SKIP'
    '8d6d99300c25d04fc92cb2e7f68ee5e8267a85c1a431abd1f23d3cb6da08db8b'
)

pkgver() {
    cd "$srcdir/$_pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_pkgname"

    sed -i "s/^PACKAGE_VERSION=.*$/PACKAGE_VERSION=$pkgver/g" dkms.conf

    git clone https://github.com/intel/ivsc-driver.git
    cp -r ivsc-driver/{backport-include,drivers,include} .
    rm -rf ivsc-driver
}

package() {
    cd "$srcdir"

    _srcdir="$pkgdir/usr/src/$_pkgname-$pkgver"
    install -dm755 "$_srcdir"
    rm -rf "$_pkgname/.git"
    cp -rT "$_pkgname" "$_srcdir"

    _udevdir="$pkgdir/usr/lib/udev/rules.d"
    install -dm755 "$_udevdir"
    install -Dm644 70-intel-ipu6-psys.rules "$_udevdir"
}
