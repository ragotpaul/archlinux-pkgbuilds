# Maintainer: Paul Ragot <ragot.paul12@gmail.com>
pkgname=gst-plugin-icamerasrc-git
_pkgname=icamerasrc
pkgver=r83.b7c78fa
pkgrel=1
pkgdesc="Multimedia graph framework - icamerasrc plugin"
arch=('x86_64')
url="https://github.com/intel/$_pkgname"
license=('LGPL-2.1-only')
groups=('ipu6-cameras')
depends=('intel-ipu6-camera-hal-git' 'gcc-libs' 'glib2' 'glibc' 'gst-plugins-bad-libs' 'gst-plugins-base-libs' 'gstreamer-vaapi' 'gstreamer' 'libdrm' 'libva')
makedepends=('git')
provides=('icamerasrc-git')
conflicts=('icamerasrc-git')
_branch=icamerasrc_slim_api
source=("$_pkgname::git+$url.git#branch=$_branch")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$_pkgname"

    export CHROME_SLIM_CAMHAL=ON

    ./autogen.sh
    ./configure --prefix=/usr --enable-gstdrmformat=yes
    make -j$(nproc)
}

package() {
    cd "$srcdir/$_pkgname"

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    DESTDIR="$pkgdir/" make install
}
