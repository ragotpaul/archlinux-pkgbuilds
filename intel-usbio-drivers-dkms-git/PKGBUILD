# Maintainer: Paul Ragot <ragot.paul12@gmail.com>
pkgname=intel-usbio-drivers-dkms-git
_pkgname=usbio-drivers
pkgver=r73.266ae66
pkgrel=1
pkgdesc="Intel USBIO Extension drivers (DKMS)"
arch=('x86_64')
url="https://github.com/intel/$_pkgname"
license=('GPL-2.0-only')
depends=('dkms')
makedepends=('git')
source=("$_pkgname::git+$url.git")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_pkgname"

    sed -i "s/^PACKAGE_VERSION=\".*\"$/PACKAGE_VERSION=\"$pkgver\"/g" dkms.conf
}

package() {
    cd "$srcdir"

    _srcdir="$pkgdir/usr/src/$_pkgname-$pkgver"
    install -dm755 "$_srcdir"
    rm -rf "$_pkgname/.git"
    cp -rT "$_pkgname" "$_srcdir"
}
