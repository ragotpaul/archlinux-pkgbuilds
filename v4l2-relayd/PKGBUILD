# Maintainer: Paul Ragot <ragot.paul12@gmail.com>
pkgname=v4l2-relayd-git
_pkgname=v4l2-relayd
pkgver=0.1.3
pkgrel=2
pkgdesc="Streaming relay for v4l2loopback using GStreamer"
arch=('x86_64')
url="https://git.launchpad.net/$_pkgname"
license=('GPL-2.0-only')
groups=('ipu6-cameras')
depends=('glib2' 'glibc' 'gst-plugin-icamerasrc-git' 'gst-plugins-base-libs' 'gstreamer' 'v4l2loopback-dkms')
makedepends=('git')
provides=('v4l2-relayd')
conflicts=('v4l2-relayd')
_branch=main
source=("$_pkgname::git+$url#branch=$_branch")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    printf "%s" "$(git describe --tags 2>/dev/null | sed 's/.*\/\([0-9\.-]*\)/\1/')"
}

build() {
    cd "$srcdir/$_pkgname"

    ./autogen.sh --prefix=/usr
    make -j$(nproc)
}

package() {
    cd "$srcdir/$_pkgname"

    make DESTDIR="$pkgdir/" install
    mv "$pkgdir/usr/etc" "$pkgdir/etc"

    _configfile="$pkgdir/etc/default/$_pkgname"
    sed -i 's/VIDEOSRC="videotestsrc"/VIDEOSRC="icamerasrc"/g' "$_configfile"
    sed -i 's/FORMAT=YUY2/FORMAT=NV12/g' "$_configfile"

    _servicefile="$pkgdir/usr/lib/systemd/system/$_pkgname.service"
    sed -i 's/! videoconvert ! v4l2sink/! videoconvert ! video\/x-raw,format=YUY2 ! v4l2sink/g' "$_servicefile"
    sed -i 's/CapabilityBoundingSet=//g' "$_servicefile"
    sed -i 's/DeviceAllow=char-video4linux//g' "$_servicefile"
}
