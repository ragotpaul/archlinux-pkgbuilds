# Maintainer: Paul Ragot <ragot.paul12@gmail.com>
pkgname=intel-ipu6-camera-hal-git
_pkgname=ipu6-camera-hal
pkgver=r111.a2de9c2
pkgrel=1
pkgdesc="Intel IPU6 camera hardware abstraction layer (Alder Lake)"
arch=('x86_64')
url="https://github.com/intel/$_pkgname"
license=('Apache-2.0')
groups=('ipu6-cameras')
depends=('intel-ipu6-camera-bins-git' 'expat' 'gcc-libs' 'glibc' 'libdrm')
makedepends=('git' 'cmake')
provides=('intel-ipu6-camera-hal-git' 'intel-ipu6ep-camera-hal-git')
conflicts=('intel-ipu6-camera-hal-git' 'intel-ipu6ep-camera-hal-git')
source=("$_pkgname::git+$url.git")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_pkgname"

    cmake -B build -S . \
        -DCMAKE_BUILD_TYPE="Release" \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_INSTALL_LIBDIR="lib" \
        -DBUILD_CAMHAL_ADAPTOR=ON \
        -DBUILD_CAMHAL_PLUGIN=ON \
        -DIPU_VERSIONS="ipu6;ipu6ep;ipu6epmtl" \
        -DUSE_PG_LITE_PIPE=ON
}

build() {
    cd "$srcdir/$_pkgname"

    cmake --build build -j$(nproc)
}

package() {
    cd "$srcdir/$_pkgname"

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    DESTDIR="$pkgdir/" cmake --install build --strip
}
