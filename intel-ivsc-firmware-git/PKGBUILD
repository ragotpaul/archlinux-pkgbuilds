# Maintainer: Paul Ragot <ragot.paul12@gmail.com>
pkgname=intel-ivsc-firmware-git
_pkgname=ivsc-firmware
pkgver=r12.74a01d1
pkgrel=1
pkgdesc="Intel Vision Sensing Controller firmware (Alder Lake)"
arch=('x86_64')
url="https://github.com/intel/$_pkgname"
license=('LicenseRef-Intel')
groups=('ipu6-cameras')
makedepends=('git')
provides=('intel-ivsc-firmware')
conflicts=('intel-ivsc-firmware')
source=("$_pkgname::git+$url.git")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "$srcdir/$_pkgname"

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    _prefix="$pkgdir/usr/lib/firmware/vsc/soc_a1_prod"
    _suffix="_a1_prod.bin"
    install -dm755 "$_prefix"
    for _binfile in $(find ./firmware -type f -name '*.bin'); do
        _basename=$(basename "$_binfile" '.bin')
        install -Dm644 "$_binfile" "$_prefix/$_basename$_suffix"
    done
}
